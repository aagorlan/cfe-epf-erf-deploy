#Использовать cmdline
#Использовать v8runner
#Использовать v8find

// Получить разрядность платформы из модуля РазрядностьПлатформы (пакет v8find)
//
// Параметры:
//   ЗначениеОпции - Строка - значение параметра --bitness x64,x86,x64x86,x86x64
//
//  Возвращаемое значение:
//   Произвольный - РазрядностьПлатформы - x64, x86, x64x86, x86x64 или пустая строка
//
Функция РазрядностьПлатформы(Знач ЗначениеОпции) Экспорт

	Если Не ЗначениеЗаполнено(ЗначениеОпции) Тогда
		Возврат Неопределено;
	ИначеЕсли ЗначениеОпции = "x64" Тогда
		Возврат РазрядностьПлатформы.x64;
	ИначеЕсли ЗначениеОпции = "x86" Тогда
		Возврат РазрядностьПлатформы.x86;
	ИначеЕсли ЗначениеОпции = "x64x86" Тогда
		Возврат РазрядностьПлатформы.x64x86;
	ИначеЕсли ЗначениеОпции = "x86x64" Тогда
		Возврат РазрядностьПлатформы.x86x64;
	Иначе
		ВызватьИсключение "Неизвестная разрядность " + ЗначениеОпции;
	КонецЕсли;

КонецФункции

Функция ИнициализацияПлатформы(ЗначенияПараметров) Экспорт

	УправлениеКонфигуратором = Новый УправлениеКонфигуратором();
	КаталогВременнойИБ = ВременныеФайлы.СоздатьКаталог();
	УправлениеКонфигуратором.КаталогСборки(КаталогВременнойИБ);

	СтрокаПодключения = ЗначенияПараметров["--ibconnection"];
	Пользователь = ЗначенияПараметров["--db-user"];
	Пароль = ЗначенияПараметров["--db-pwd"];

	УправлениеКонфигуратором.УстановитьКонтекст(СтрокаПодключения, Пользователь, Пароль);

	ВерсияПлатформы = ЗначенияПараметров["--v8version"];

	Если НЕ ПустаяСтрока(ВерсияПлатформы) Тогда
		Разрядность = ЗначенияПараметров["--bitness"];
		Разрядность = РазрядностьПлатформы(Разрядность);
		УправлениеКонфигуратором.ИспользоватьВерсиюПлатформы(ВерсияПлатформы, Разрядность);
	КонецЕсли;

	КлючРазрешенияЗапуска = ЗначенияПараметров["--uccode"];

	Если Не ПустаяСтрока(КлючРазрешенияЗапуска) Тогда
		УправлениеКонфигуратором.УстановитьКлючРазрешенияЗапуска(КлючРазрешенияЗапуска);
	КонецЕсли;

	Возврат УправлениеКонфигуратором;

КонецФункции

Процедура ОбновитьПараметрыПеременнымиОкружения(ПараметрыКоманды) Экспорт
	СоответствиеПеременных = Новый Соответствие();

	СоответствиеПеременных.Вставить("RUNNER_IBCONNECTION", "--ibconnection");
	СоответствиеПеременных.Вставить("RUNNER_DBUSER", "--db-user");
	СоответствиеПеременных.Вставить("RUNNER_DBPWD", "--db-pwd");
	СоответствиеПеременных.Вставить("RUNNER_v8version", "--v8version");
	СоответствиеПеременных.Вставить("RUNNER_uccode", "--uccode");

	СоответствиеПеременных.Вставить("RUNNER_V8VERSION", "--v8version");
	СоответствиеПеременных.Вставить("RUNNER_UCCODE", "--uccode");

	Для каждого Элемент Из СоответствиеПеременных Цикл
		Если ЗначениеЗаполнено(ПараметрыКоманды[Элемент.Значение]) Тогда
			Продолжить;
		КонецЕсли;
		ЗначениеПеременной = ПолучитьПеременнуюСреды(ВРег(Элемент.Ключ));
		Если ЗначениеПеременной <> Неопределено Тогда
			Если ЗначениеПеременной = """""" Или ЗначениеПеременной = "''" Тогда
				ЗначениеПеременной = "";
			КонецЕсли;
			ПараметрыКоманды.Вставить(Элемент.Значение, ЗначениеПеременной);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры